! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

! Symposium for Standardisation of the Aromanian Writing System in Bitola in 1997:
! a, ã, b, c, d, dh, dz, e, f, g, h, i, j, k, l, lj, m, n, nj, o, p, q, r, s, sh, t, th, ts, u, v, w, x, y, z



!0	zero
!1	unã
!2	dau
!3	trei
!4	patru
!5	tsintsi
!6	shasi  (ja, det kan være mange sub-former)
!7	shapti (sub-forms?)
!8	optu
!9	nau
!10	dzatsi
!11	unãsprã
!12	dausprã
!13	treisprã
!14	pasprã
!15	tsisprã
!16	shasprã
!17	shaptisprã
!18	optusprã
!19	nausprã
!20	yinghits
!21  unãsprãyinghits
!22  dausprãyinghits
!30	treidzãts
!43  patrudzãtsãtrei
!55  tsindzãtsãtsintsi
!65  shaidzãtsãtsintsi
!71  shaptidzãtsãunã
!78  shaptidzãtsãoptu
!87  obdzãtsãshapti
!98  naudzãtsãoptu
!100 unã sutã
!200 dau suti
!101 unã sutã sh-unã
!213 dau suti treisprã
!1000 unã njilji

!something to debug
!xfst[1]: up 222
!dau suti dausprãyinghits
!daunjiljidausprãyinghits
!xfst[1]: up 2222
!daunjiljidau suti dausprãyinghits
!yinghitsdaunjiljidausprãyinghits
!xfst[1]: up 22222
!dausutãyinghitsdaunjiljidausprãyinghits
!yinghitsdaunjiljidau suti dausprãyinghits


!Multichar_Symbols
!  +Use/NG    ! Do not generate, for isme-ped.fst and apertium
!  +String    ! Tag to denote non-numeric strings
!  +NumNum    ! Tag to denote real numbers in one form or another

LEXICON Root

! This part for TTS, ignore
! < %+String [a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|x|y|z|ã|A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Ã]* > COMMA ; ! This first line is to allow all letter strings.
! +NumNum NUMBERSECTION ;
! +NumNum COMMASECTION ;
! 
! LEXICON NUMBERSECTION

HUNDREDSM ;		! 200M
1:sutã  HUNDREDM ;	! 100M
TENSM ;			! 20-99M
TEENSM ;		! 10-19M
ONESM ;			! 1-9M
HUNDREDST ;		! 200000-999999
1:sutã  HUNDREDT ;	! 100000-100999
TENST ;			! 20000-99999,10000-10999
TEENST ;		! 11000-19999
ONEST ;			! 2000-9999
1:unã% njilji  THOUSAND ;	! 1000-1999
UNDERTHOUSAND ;		! 100-999
HUNDRED ;
TENS ;			! 20-99
TEENS ;			! 10-19
ONES ;			! 1-9

LEXICON HUNDREDSM
2:dau   HNDRM ;
3:trei  HNDRM ;
4:patru  HNDRM ; ! Google: 459
5:tsintsi  HNDRM ;
6:shasi  HNDRM ;
7:shapti  HNDRM ;
8:optu  HNDRM ;
9:nau  HNDRM ;

LEXICON HNDRM
:sutã  HUNDREDM ;

LEXICON HUNDREDM
      TENSM ;
      TEENSM ;
%0:   ONESM ;
%0%0: MILJON ;

LEXICON TEENSM
1:dzatsi  TEENM ;

LEXICON TEENM
1:unã      TEENS_2ND_PART_M ;
2:dau    TEENS_2ND_PART_M ;
3:trei     TEENS_2ND_PART_M ;
4:patru  TEENS_2ND_PART_M ;
5:tsintsi    TEENS_2ND_PART_M ;
6:shasi    TEENS_2ND_PART_M ;
7:shapti  TEENS_2ND_PART_M ;
8:optu  TEENS_2ND_PART_M ;
9:nau    TEENS_2ND_PART_M ;

LEXICON TEENS_2ND_PART_M
MILJON ;

LEXICON TENSM
1%0:dzatsi MILJON ;
2:dau    TNM ;
3:trei     TNM ;
4:patru  TNM ;
5:tsintsi    TNM ;
6:shasi    TNM ;
7:shapti  TNM ;
8:optu  TNM ;
9:nau    TNM ;

LEXICON TNM
%0:dzatsi MILJON ;
:dzatsi   ONESM ;

LEXICON ONESM
1:unã% miljon%   OVERTHOUSANDS ;
2:dau          MILJON ;
3:trei           MILJON ;
4:patru        MILJON ;
5:tsintsi          MILJON ;
6:shasi          MILJON ;
7:shapti        MILJON ;
8:optu        MILJON ;
9:nau          MILJON ; 

LEXICON MILJON
:% miljona%   OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERTHOUSANDS
HUNDREDST ;		! x00.000.000
1:sutã  HUNDREDT ;	! 100.000.000
%0: TENST ;		!  20.000.000
%0: TEENST ;		!  10.000.000
%0%0: ONEST ;		!   2.000.000
%0%01:njilji THOUSAND ;	!   1.000.000
%0%0%0: UNDERTHOUSAND ;	
%0%0%0%0: TENS ;
%0%0%0%0: TEENS ;
%0%0%0%0%0: ONES ;
%0%0%0%0%0%0: COMMA ;	!  x000000

LEXICON HUNDREDST
2:dau    HNDRT ;	! 200000-299999
3:trei     HNDRT ;	! 300000-399999
4:patru  HNDRT ;	! 400000-499999
5:tsintsi    HNDRT ;	! 500000-599999
6:shasi    HNDRT ;	! 600000-699999
7:shapti  HNDRT ;	! 700000-799999
8:optu  HNDRT ;	! 800000-899999
9:nau    HNDRT ;	! 900000-999999

LEXICON HNDRT
:sutã  HUNDREDT ;	! 100000-199999

LEXICON HUNDREDT	! X = 1-9, Y = 0-9
TENST ;			! X2XYYY, X10YYY
TEENST ;		! X1XYYY
%01:unã THOUSANDS ;
%0: ONEST ;		! XX0YYY
%0%0: THOUSANDS ;	! X00YYY

LEXICON TEENST
1:dzatsi    TEENT ;
1%0:dzatsi  THOUSANDS ;

LEXICON TEENT
1:unã      TEENS_2ND_PART_T ;
2:dau    TEENS_2ND_PART_T ;
3:trei     TEENS_2ND_PART_T ;
4:patru  TEENS_2ND_PART_T ;
5:tsintsi    TEENS_2ND_PART_T ;
6:shasi    TEENS_2ND_PART_T ;
7:shapti  TEENS_2ND_PART_T ;
8:optu  TEENS_2ND_PART_T ;
9:nau    TEENS_2ND_PART_T ;

LEXICON TEENS_2ND_PART_T
THOUSANDS ;

LEXICON TENST
2:yinghits    TNT ;
3:treidzãts     TNT ;
4:patrudzãts  TNT ;
5:tsindzãts    TNT ;
6:shaidzãts    TNT ;
7:shaptidzãts  TNT ;
8:obdzãts  TNT ;
9:naudzãts    TNT ;

LEXICON TNT
%0:   THOUSANDS ;
      ONEST ;

LEXICON ONEST
1:unã%   THOUSANDS ; !unã njilji 1000
2:dau  THOUSANDS ;
3:trei  THOUSANDS ;
4:patru  THOUSANDS ;
5:tsintsi  THOUSANDS ;
6:shasi  THOUSANDS ;
7:shapti  THOUSANDS ;
8:optu  THOUSANDS ;
9:nau  THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
:njilji  THOUSAND ;

LEXICON THOUSAND
UNDERTHOUSAND ;	   ! 1100-1999
%0: TENS ;	   ! 1020-1099
%0: TEENS ;	   ! 1010-1019
%0%0: ONES ;	   ! 1001-1009
%0%0%0: COMMA ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================


! From here on only 1-999

! The lexicon UNDERTHOUSAND takes care of
! the hundred-digit

LEXICON UNDERTHOUSAND
%0: HUNDRED ;
1:unã%   HNDR_S ; ! 100 cardinal
2:dau%     HNDR_P ;
3:trei%      HNDR_P ;
4:patru%   HNDR_P ;
5:tsintsi%     HNDR_P ;
6:shasi%     HNDR_P ;
7:shapti%   HNDR_P ;
8:optu%   HNDR_P ;
9:nau%     HNDR_P ;


LEXICON HNDR_S ! Here we add the word for hundred
:sutã%     HUNDRED ;
%0%0:sutã COMMA ; ! precisely 100, 200, 300...
%0:sutã% sh%-    ONES ; ! continue to 101, 102, ... 
!tjuatede:  CUODAAT ; ! ordinal hundredth, wait with ordinals

LEXICON HNDR_P ! Here we add the word for hundred
:suti%    HUNDRED ;
%0%0:suti COMMA ; ! precisely 100, 200, 300...
%0:suti    ONES ; ! continue to 101, 102, ... 
!tjuatede:  CUODAAT ; ! ordinal hundredth, wait with ordinals


LEXICON HUNDRED
 TEENS ;    ! continue to 111, 112, ...
 TWENTIES ; ! continue to 20, 21, ...
 TENS ;     ! continue to 30, 31, ..., 40, 41, ...

! LEXICON CUODAAT  ordinals, 
! :%0%0%.    COMMA ;

LEXICON TEENS
1%0:dzatsi   COMMA ;   
1: TEENSNUMBERS ;

LEXICON TEENSNUMBERS
1:unã  TEENSEND ;
2:dau  TEENSEND ;
3:trei  TEENSEND ;
4:pa  TEENSEND ;
5:tsi  TEENSEND ;
6:sha  TEENSEND ;
7:shapti  TEENSEND ;
8:optu  TEENSEND ;
9:nau  TEENSEND ;

LEXICON TEENSEND
:sprã  COMMA ;

LEXICON TWENTIES
2%0:yinghits   COMMA ;   
2: TWENTIESNUMBERS ;

LEXICON TWENTIESNUMBERS 
1:unã  TWENTIESEND ;
2:dau  TWENTIESEND ;
3:trei  TWENTIESEND ;
4:pa  TWENTIESEND ;
5:tsi  TWENTIESEND ;
6:sha  TWENTIESEND ;
7:shapti  TWENTIESEND ;
8:optu  TWENTIESEND ;
9:nau  TWENTIESEND ;

LEXICON TWENTIESEND
:sprãyinghits  COMMA ;

LEXICON TENS
1%0:dzatsi  COMMA ;
2%0:yinghits   COMMA ; 
3:treidzãts     TN ;
4:patrudzãts  TN ;
5:tsindzãts    TN ;
6:shaidzãts    TN ;
7:shaptidzãts  TN ;
8:obdzãts  TN ;
9:naudzãts    TN ;

LEXICON TN
%0:   COMMA ;
%0%.: COMMA ;
:ã     ONES ; ! Fugenvokal


LEXICON ONES
CARDINAL ;
!ORDINAL ;

LEXICON CARDINAL
!zero:%0 COMMA ;
1:unã  COMMA ;
2:dau  COMMA ;
3:trei  COMMA ;
4:patru  COMMA ;
5:tsintsi  COMMA ;
6:shasi  COMMA ;
7:shapti  COMMA ;
8:optu  COMMA ;
9:nau  COMMA ;

!LEXICON ORDINAL
!voestes:1%.     COMMA ;
!mubpie:2%.   COMMA ;
!gåalmede:3%.   COMMA ;
!njealjede:4%. COMMA ;
!tsintside:5%.   COMMA ;
!shaside:6%.   COMMA ;
!shaptide:7%.    COMMA ;
!gaaktsede:8%.   COMMA ;
!åktsede:9%.    COMMA ;

LEXICON COMMA
 ENDLEX ;
 COMMASECTION ;

LEXICON COMMASECTION  ! these are not spellchecked!!!
                  ENDLEX ;
! % komma% :,      Root ;  ! for TTS, commented out
! % tjuohkkis% :%. Root ;
! % kolon% :%:     Root ;
! % sárggis% :%-   Root ; 
!% la% :%=         Root ; 
!% gráda% :%°      Root ; 
!% paragráfa% :§   Root ; 
! % násti% :%*     Root ; 
!% ja% :&          Root ; 

LEXICON ENDLEX
 # ;
